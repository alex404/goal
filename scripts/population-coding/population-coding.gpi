# Preamble
set loadpath load_path
set datafile separator ","
set terminal pngcairo dashed enhanced size 1000,600 font "Vera,24"

# Stats
csv1 = "population-coding-1.csv"
csv2 = "population-coding-2.csv"
stats csv1 nooutput
ncls = STATS_columns

# Labels
set xlabel 'Orientation'
set ytics nomirror
set ylabel 'Rate'
set y2label 'Total Rate'
set xtics ("0" 0,"0.5π" pi/2, "π" pi, "1.5π" 1.5*pi, "2π" 2*pi)
set y2tics
set xrange [0:2*pi]
set yrange [0:10]
set y2range [0:35]

set output "population-coding-1.png"

# Plot
plot csv1 using 1:2 with lines axis x1y2 lw 4 title "Sum of Tuning Curves", \
     csv1 using 1:3 with lines axis x1y2 lw 4 dt (25,25) title "Sinusoid Fit", \
     csv1 using 1:4 with lines lw 4 lc 'black' title "Tuning Curves",\
     for [i=5:ncls] csv1 using 1:i with lines lw 4 lc 'black' notitle,\

set output "population-coding-2.png"

# Plot
plot csv2 using 1:2 with lines axis x1y2 lw 4 title "Sum of Tuning Curves", \
     csv2 using 1:3 with lines axis x1y2 lw 4 dt (25,25) title "Sinusoid Fit", \
     csv2 using 1:4 with lines lw 4 lc 'black' title "Tuning Curves",\
     for [i=5:ncls] csv2 using 1:i with lines lw 4 lc 'black' notitle,\
