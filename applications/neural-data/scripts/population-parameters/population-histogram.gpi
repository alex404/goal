# Preamble
cd load_path
set output "population-histogram.png"

# Settings
set key autotitle columnhead
set boxwidth 0.8 relative
set style fill solid 0.5

# Variables
nmix = 3
nrws = (nmix+1)/2 + 1

# Plot
set multiplot layout nrws,2

set xtics ("0" 0,"0.5π" pi/2, "π" pi, "1.5π" 1.5*pi, "2π" 2*pi)
set xrange [0:2*pi]
set yrange [0:]

plot "preferred-stimulus-histogram.csv" using 1:3 with boxes title "Preferred Stimuli" lc black, \
     "preferred-stimulus-fit.csv" using 1:2 with lines lc 'black' lw 5, \
     "preferred-stimulus-fit.csv" using 1:3 with lines lc 'red' lw 5

unset xrange
unset xtics
set xtics

plot "precision-histogram.csv" using 1:3 with boxes title "Precisions" lc black, \
     "precision-fit.csv" index 7 using 1:2 with lines lc 'black' lw 5, \
     "precision-fit.csv" index 7 using 1:3 with lines lc 'red' lw 5

do for [i=0:nmix-1]{
    plot sprintf("gain-histogram-%d.csv",i) using 1:3 with boxes title "Gains" lc black, \
         sprintf("gain-fit-%d.csv",i) using 1:2 with lines lc 'black' lw 5, \
         sprintf("gain-fit-%d.csv",i) using 1:3 with lines lc 'red' lw 5
}
unset multiplot
