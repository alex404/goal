# Preamble
plotname = 'tuning-curves'
pngheight=800
pngwidth=1200
load preload

# STATS
stats csv nooutput
n = STATS_blocks-2
ncls = ceil(sqrt(n))
nrws = floor(sqrt(n))
if (nrws*ncls < n) nrws = nrws+1

stats csv index 2 nooutput
k = STATS_columns-2

# Multiplot
set multiplot layout nrws,ncls title "Gain-Modulated Tuning Curves"

# Axes
set xtics nomirror ("0" 0,"0.5π" pi/2, "π" pi, "1.5π" 1.5*pi, "2π" 2*pi)
set xrange [0:2*pi]

do for [i = 2:n+1] {
    plot for [j = 2:(k+1)] csv index i using 1:j with lines notitle
}
unset multiplot
